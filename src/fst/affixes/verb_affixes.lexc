! Blackfoot Regular Animate Intransitive Verb Inflectional Morphology

Multichar_Symbols
+VAI
+VII
+VTI
+VTA
Adv+
Neg+
Perf+
Dur+
Fut+
Prox+
Past+
Nonaff+
+Pl
+Sg
+Imp
+Ind
+Cnj
+Sbj
+Irr
+Pre
+5
+Der/Caus
+Der/Rcpr
+Der/Rflx
+Der/Bnf
+Unspec ! +X
+Accomp
+0Sg
+0Pl
+1Sg
+2Sg
+3Sg
+1Pl
+21Pl
+2Pl
+3Pl
+4Sg
+4Pl

! Flag diacritics to be used
@R.accomp.A@
@R.person.NI@
@R.person.KI@
@R.person.KII@
@R.person.OT@
@R.person.YI@
@R.person.IMP@
@R.person.NULL@
@P.person.NI@
@P.person.KI@
@P.person.KII@
@P.person.OT@
@P.person.YI@
@P.person.IMP@
@P.person.NULL@
@R.mode.IND@
@R.mode.CON@
@R.mode.SUB@
@R.mode.IRR@
@R.mode.IMP@
@P.mode.IND@
@P.mode.CON@
@P.mode.SUB@
@P.mode.IRR@
@P.mode.IMP@
@D.person.NULL@
@D.person.OT@
@P.class.VAI@
@P.class.VII@
@P.class.VTI@
@P.class.VTA@
@R.class.VAI@
@R.class.VII@
@R.class.VTI@
@R.class.VTA@
@P.assoc.A@
@R.assoc.A@

! Special_characters
i3
i2
n2
m2
X
%<
%>

! Desired input-output
! Input: @U.person.NI@ 0
! Output: @U.person.NI@ nit 
! -> LEXC: @U.person.NI@0:@U.person.NI@ nit

LEXICON Root
PREFIX_SETS ;

LEXICON PREFIX_SETS
@P.class.VAI@	BASIC_PREFIXES ;
@P.class.VII@	BASIC_PREFIXES ;
@P.class.VTI@	BASIC_PREFIXES ;
@P.class.VTA@	BASIC_PREFIXES ;

LEXICON BASIC_PREFIXES
@P.mode.IND@	BASIC_IND_PREFIXES ;
@P.mode.CON@	BASIC_CNJ_PREFIXES ;
@P.mode.SUB@	BASIC_SBJ_PREFIXES ;
@P.mode.IRR@	BASIC_IRR_PREFIXES ;
@P.mode.IMP@	BASIC_IMP_PREFIXES ;

LEXICON BASIC_IND_PREFIXES
@P.person.NI@:@P.person.NI@nit%< PREFIXES ;
@P.person.KI@:@P.person.KI@kit%< PREFIXES ;
@P.person.OT@:@P.person.OT@kit%< PREFIXES ;
@P.person.NULL@:@P.person.NULL@0 PREFIXES ;

LEXICON BASIC_CNJ_PREFIXES
@P.person.NI@:@P.person.NI@nit%<    PREFIXES ;
@P.person.KI@:@P.person.KI@kit%<    PREFIXES ;
@P.person.OT@:@P.person.OT@ot%<     PREFIXES ;
@P.person.NULL@:@P.person.NULL@0    PREFIXES ;

LEXICON BASIC_SBJ_PREFIXES
@P.person.NULL@:@P.person.NULL@0	PREFIXES ;

LEXICON BASIC_IRR_PREFIXES
@P.person.NI@:@P.person.NI@nit%<    PREFIXES ;
@P.person.KI@:@P.person.KI@kit%<    PREFIXES ;
@P.person.NULL@:@P.person.NULL@0    PREFIXES ;

LEXICON BASIC_IMP_PREFIXES
@P.person.IMP@:@P.person.IMP@0    PREFIXES ;

LEXICON PREFIXES
0                                     VERB_STEMS ;
@R.mode.IND@Neg+:@R.mode.IND@máát%<   PREFIXES ;
@R.mode.IMP@Neg+:@R.mode.IMP@miin%<   PREFIXES ;
@R.mode.IMP@Neg+:@R.mode.IMP@piin%<   PREFIXES ;
@P.assoc.A@:@P.assoc.A@Xohpok%<       BOUND ;
@R.mode.CON@:@R.mode.CON@saw%<        BOUND ;
@R.mode.SUB@:@R.mode.SUB@saw%<        BOUND ;
@R.mode.CON@:@R.mode.CON@áákssta7%<   BOUND ;
@R.mode.CON@:@R.mode.CON@ááhkssta7%<  BOUND ;
@R.mode.IRR@:@R.mode.IRR@kátá7%<      BOUND ;
wayák:wayák	BOUND ;
yáak:yáak	BOUND ;
ayáak:ayáak	BOUND ;
akaa:akaa	BOUND ;
Xomohp:Xomohp	BOUND ;
Xomoht:Xomoht	BOUND ;
Xohtatsiwa:Xohtatsiwa	BOUND ;
itap:itap	BOUND ;
poohsap:poohsap	BOUND ;
i2poohsap:i2poohsap	BOUND ;
it:it	BOUND ;
ist:ist	BOUND ;
ípssit:ípssit	BOUND ;
ohkit:ohkit	BOUND ;
miistap:miistap	BOUND ;
yi2i2stap:yi2i2stap	BOUND ;
saaki:saaki	BOUND ;
omá:omá	BOUND ;
iimá:iimá	BOUND ;
á7:á7	BOUND ;
á:á	BOUND ;
ááhk:ááhk	BOUND ;
iiht:iiht	BOUND ;
oht:oht	BOUND ;
omoht:omoht	BOUND ;
á7:á7	BOUND ;
á7:á7	BOUND ;
ikkam:ikkam	BOUND ;
Xoohk:Xoohk	BOUND ;
Xipp:Xipp	BOUND ;
iik:iik	BOUND ;
sska7:sska7	BOUND ;
sstónnat:sstónnat	BOUND ;
i2kkam:i2kkam	BOUND ;
iitsiksist:iitsiksist	BOUND ;
ii:ii	BOUND ;
na:na	BOUND ;
hkana:hkana	BOUND ;
i2kkina:i2kkina	BOUND ;
iiyik:iiyik	BOUND ;
i2poina:i2poina	BOUND ;
poína:poína	BOUND ;
sok:sok	BOUND ;
niit:niit	BOUND ;
anist:anist	BOUND ;
ohs:ohs	BOUND ;
yaahs:yaahs	BOUND ;
i2ksistt:i2ksistt	BOUND ;
mato:mato	BOUND ;
oto:oto	BOUND ;
ohkott:ohkott	BOUND ;
ssáak:ssáak	BOUND ;
ohko:ohko	BOUND ;
á:á	BOUND ;
a:a	BOUND ;
á’:á’	BOUND ;
a’kooht:a’kooht	BOUND ;
á’p:á’p	BOUND ;
ááhk:ááhk	BOUND ;
ááhkamá’p:ááhkamá’p	BOUND ;
aahksikkam:aahksikkam	BOUND ;
ááhksikkama’p:ááhksikkama’p	BOUND ;
aahs:aahs	BOUND ;
áák:áák	BOUND ;
aaksta’:aaksta’	BOUND ;
aanist:aanist	BOUND ;
ai’tamáak:ai’tamáak	BOUND ;
áísooka:áísooka	BOUND ;
aisskahs:aisskahs	BOUND ;
áka:áka	BOUND ;
ákáá:ákáá	BOUND ;
akisspí:akisspí	BOUND ;
akk:akk	BOUND ;
akoksska:akoksska	BOUND ;
ákstam:ákstam	BOUND ;
ama:ama	BOUND ;
amo’tap:amo’tap	BOUND ;
ana:ana	BOUND ;
anaw:anaw	BOUND ;
anay:anay	BOUND ;
ano:ano	BOUND ;
anow:anow	BOUND ;
anoy:anoy	BOUND ;
anootap:anootap	BOUND ;
ao’tamáak:ao’tamáak	BOUND ;
apa’p:apa’p	BOUND ;
aso:aso	BOUND ;
assaak:assaak	BOUND ;
att:att	BOUND ;
awa’:awa’	BOUND ;
ayaak:ayaak	BOUND ;
ayak:ayak	BOUND ;
i’sskaan:i’sskaan	BOUND ;
i’tsk:i’tsk	BOUND ;
iámoohk:iámoohk	BOUND ;
ihta:ihta	BOUND ;
iihk:iihk	BOUND ;
iihtsitok:iihtsitok	BOUND ;
iihtsitssitak:iihtsitssitak	BOUND ;
iik:iik	BOUND ;
iikska’:iikska’	BOUND ;
iima:iima	BOUND ;
iimaa:iimaa	BOUND ;
iimat:iimat	BOUND ;
iin:iin	BOUND ;
iina:iina	BOUND ;
iinao’k:iinao’k	BOUND ;
iinatt:iinatt	BOUND ;
iini:iini	BOUND ;
iinik:iinik	BOUND ;
iitaisap:iitaisap	BOUND ;
iitsiksist:iitsiksist	BOUND ;
iitskimaan:iitskimaan	BOUND ;
iiyik:iiyik	BOUND ;
íka:íka	BOUND ;
ika’:ika’	BOUND ;
ika’to:ika’to	BOUND ;
ikaap:ikaap	BOUND ;
ikaiksist:ikaiksist	BOUND ;
ikak:ikak	BOUND ;
ikamo’t:ikamo’t	BOUND ;
ikapo:ikapo	BOUND ;
ikayiss:ikayiss	BOUND ;
ikia’ya:ikia’ya	BOUND ;
ikihkiht:ikihkiht	BOUND ;
ikimm:ikimm	BOUND ;
ikimmat:ikimmat	BOUND ;
ikipp:ikipp	BOUND ;
ikippa’:ikippa’	BOUND ;
ikíppoa:ikíppoa	BOUND ;
ikkam:ikkam	BOUND ;
ikkam:ikkam	BOUND ;
ikkan:ikkan	BOUND ;
ikkia’:ikkia’	BOUND ;
ikkihkini:ikkihkini	BOUND ;
ikkina:ikkina	BOUND ;
iksissto:iksissto	BOUND ;
iksissto:iksissto	BOUND ;
iksist:iksist	BOUND ;
iksksowa’:iksksowa’	BOUND ;
ikssto:ikssto	BOUND ;
ikstsina’:ikstsina’	BOUND ;
imaat:imaat	BOUND ;
imak:imak	BOUND ;
immoyi:immoyi	BOUND ;
inai’t:inai’t	BOUND ;
inikk:inikk	BOUND ;
innaahkoot:innaahkoot	BOUND ;
ipahkitap:ipahkitap	BOUND ;
ipahtsik:ipahtsik	BOUND ;
ipákkssa:ipákkssa	BOUND ;
ipakska:ipakska	BOUND ;
ipana:ipana	BOUND ;
ipann:ipann	BOUND ;
ipapa:ipapa	BOUND ;
ipiitsiiyóóhk:ipiitsiiyóóhk	BOUND ;
ipiitsiy:ipiitsiy	BOUND ;
ipo’t:ipo’t	BOUND ;
ipoina:ipoina	BOUND ;
ipp:ipp	BOUND ;
ippita:ippita	BOUND ;
ippoa:ippoa	BOUND ;
ippssa’kapp:ippssa’kapp	BOUND ;
ipsstsík:ipsstsík	BOUND ;
ipsstsikatt:ipsstsikatt	BOUND ;
isimi:isimi	BOUND ;
isinap:isinap	BOUND ;
isitsip:isitsip	BOUND ;
isk:isk	BOUND ;
iss:iss	BOUND ;
isska’:isska’	BOUND ;
isstohkana:isstohkana	BOUND ;
ista’:ista’	BOUND ;
istohk:istohk	BOUND ;
istohkana:istohkana	BOUND ;
istsa’:istsa’	BOUND ;
istto’t:istto’t	BOUND ;
isttsikohpo:isttsikohpo	BOUND ;
isttsitsa:isttsitsa	BOUND ;
isttssi:isttssi	BOUND ;
itáak:itáak	BOUND ;
ito’t:ito’t	BOUND ;
iton:iton	BOUND ;
itsisa:itsisa	BOUND ;
itsk:itsk	BOUND ;
itsst:itsst	BOUND ;
káák:káák	BOUND ;
kaat:kaat	BOUND ;
káátsisto:káátsisto	BOUND ;
káátsisto:káátsisto	BOUND ;
kakanott:kakanott	BOUND ;
kam:kam	BOUND ;
kan:kan	BOUND ;
káta’:káta’	BOUND ;
kínnak:kínnak	BOUND ;
kipita:kipita	BOUND ;
kipp:kipp	BOUND ;
kotski:kotski	BOUND ;
ksisttoyi:ksisttoyi	BOUND ;
maak:maak	BOUND ;
máak:máak	BOUND ;
maam:maam	BOUND ;
maat:m2aat	BOUND ;
máát:m2áát  BOUND ;
máátaommita’p:m2áátaommita’p	BOUND ;
mak:mak	BOUND ;
máo’k:máo’k	BOUND ;
mato:mato	BOUND ;
matsowá’p:matsowá’p	BOUND ;
matt:m2att	BOUND ;
mátt:m2átt	BOUND ;
mattsistá’:m2attsistá’	BOUND ;
máttsit:m2áttsit	BOUND ;
mi’kotsi:mi’kotsi	BOUND ;
miiksist:miiksist	BOUND ;
miin:miin	BOUND ;
min:min	BOUND ;
missk:missk	BOUND ;
misst:misst	BOUND ;
mo’tóí:mo’tóí	BOUND ;
naat:naat	BOUND ;
nai’t:nai’t	BOUND ;
niistsikáp:niistsikáp	BOUND ;
niit:niit	BOUND ;
niit:niit	BOUND ;
niitá’p:niitá’p	BOUND ;
níítaak:níítaak	BOUND ;
niitsi:niitsi	BOUND ;
niitsskimaan:niitsskimaan	BOUND ;
noohk:noohk	BOUND ;
noohkátt:noohkátt	BOUND ;
o’kia’p:o’kia’p	BOUND ;
o’kio:o’kio	BOUND ;
o’tap:o’tap	BOUND ;
o’to:o’to	BOUND ;
ohk:ohk	BOUND ;
ohkan:ohkan	BOUND ;
ohkana:ohkana	BOUND ;
ohko:ohko	BOUND ;
ohkott:ohkott	BOUND ;
ohpii:ohpii	BOUND ;
ohpiistap:ohpiistap	BOUND ;
ohpo’kii:ohpo’kii	BOUND ;
ohpok:ohpok	BOUND ;
oka’p:oka’p	BOUND ;
oma:oma	BOUND ;
omaa:omaa	BOUND ;
omahka:omahka	BOUND ;
omanista:omanista	BOUND ;
omat:omat	BOUND ;
omi:omi	BOUND ;
omiiksist:omiiksist	BOUND ;
omo’tap:omo’tap	BOUND ;
on:on	BOUND ;
ona’:ona’	BOUND ;
onai’t:onai’t	BOUND ;
onaki:onaki	BOUND ;
oohk:oohk	BOUND ;
oohkanistta:oohkanistta	BOUND ;
oohkatt:oohkatt	BOUND ;
opaksikssto:opaksikssto	BOUND ;
otam:otam	BOUND ;
oto:oto	BOUND ;
otomohk:otomohk	BOUND ;
piin:piin	BOUND ;
piitsiyoohk:piitsiyoohk	BOUND ;
pin:pin	BOUND ;
po’kí:po’kí	BOUND ;
poina:poina	BOUND ;
sa:sa	BOUND ;
sa:sa	BOUND ;
saa:saa	BOUND ;
saaki:saaki	BOUND ;
saapo’kit:saapo’kit	BOUND ;
saipa’:saipa’	BOUND ;
sáótat:sáótat	BOUND ;
sipi:sipi	BOUND ;
sook:sook	BOUND ;
sootam:sootam	BOUND ;
sotám:sotám	BOUND ;
soyi:soyi	BOUND ;
ssáak:ssáak	BOUND ;
ssap:ssap	BOUND ;
sska’:sska’	BOUND ;
sspi:sspi	BOUND ;
sstahpik:sstahpik	BOUND ;
sstok:sstok	BOUND ;
sstonnat:sstonnat	BOUND ;
sstsina’:sstsina’	BOUND ;
stam:stam	BOUND ;
tatsiki:tatsiki	BOUND ;
waahki:waahki	BOUND ;
waahko:waahko	BOUND ;
waahto:waahto	BOUND ;
waahtsam:waahtsam	BOUND ;
waahtso:waahtso	BOUND ;
waako’tsi:waako’tsi	BOUND ;
waaksisto:waaksisto	BOUND ;
waanaoyit:waanaoyit	BOUND ;
waanook:waanook	BOUND ;
waapiiwaan:waapiiwaan	BOUND ;
wáyák:wáyák	BOUND ;
wayákiit:wayákiit	BOUND ;
yáak:yáak	BOUND ;
yámoohk:yámoohk	BOUND ;
yoisstsikina:yoisstsikina	BOUND ;
yook:yook	BOUND ;

LEXICON BOUND
+:%< PREFIXES ;

! Blackfoot Regular VAI Stems

! Below are the regular VAI stems and their past tense forms. 
! They are organized as prefixed and non-prefixed forms 

LEXICON VERB_STEMS
@R.class.VAI@	VAI_STEMS ;
@R.class.VII@	VII_STEMS ;
@R.class.VTI@	VTI_STEMS ;
@R.class.VTA@	VTA_STEMS ;

!! Now we collect the verb stems !!

! I am going to include the concrete verb finals (see Frantz 2017 chapter 18.)
! The reason being that this is the best place to control them considering the four verb classes
! The relfexive changes VTA verbs into VAI verbs and takes VAI suffixes. The same is true of the reciprocal. 
! The benfactive changes Intransitive to Transitive verbs.

LEXICON VAI
+VAI:0                                        VAI_SUFFIXES ;
+VAI+Der/Caus:%>áttsi                         VTA ;
+VAI+Der/Caus:%>pi                            VTA ;
@P.class.VTA@+VAI+Der/Bnf:@P.class.VTA@%>mo   VTA ;
@P.class.VTA@+VAI+Der/Bnf:@P.class.VTA@%>omo  VTA ;
@P.class.VTA@+VAI+Der/Bnf:@P.class.VTA@%>o    VTA ;
@R.accomp.A@+VAI+Accomp:@R.accomp.A@%>m	      VAI_SUFFIXES ; ! -> VTA?

LEXICON VII
+VII:0		VII_SUFFIXES ;

LEXICON VTI
+VTI:0                                        VTI_SUFFIXES ;
@P.class.VTA@+VTI+Der/Bnf:@P.class.VTA@%>mo   VTA ;
@P.class.VTA@+VTI+Der/Bnf:@P.class.VTA@%>omo  VTA ;
@P.class.VTA@+VTI+Der/Bnf:@P.class.VTA@%>o    VTA ;

LEXICON VTA
+VTA:0                        VTA_SUFFIXES ;
+VTA+Der/Bnf+VTA:%>mo         VTA_SUFFIXES ;
+VTA+Der/Bnf+VTA:%>omo        VTA_SUFFIXES ;
+VTA+Der/Bnf+VTA:%>o          VTA_SUFFIXES ;
+VTA+Der/Rflx+VAI:%>ohsi      VAI_SUFFIXES ;
+VTA+Der/Rcpr+VAI:%>otsiiyi   VAI_SUFFIXES ;
+VTA+Der/Rcpr+VAI:%>tsiiyi    VAI_SUFFIXES ;

! BELOW ARE THE VAI SUFFIXES ;

LEXICON VAI_SUFFIXES
@R.mode.IND@+Ind:@R.mode.IND@ INDEP_VAI_SUFFIXES ;
@R.mode.CON@+Cnj:@R.mode.CON@ CONJ_VAI_SUFFIXES ;
@R.mode.SUB@+Sbj:@R.mode.SUB@ SUBJ_VAI_SUFFIXES ;
@R.mode.IRR@+Irr:@R.mode.IRR@ UNREAL_VAI_SUFFIXES ;
@R.mode.IMP@+Imp:@R.mode.IMP@ IMP_VAI_SUFFIXES ;

LEXICON INDEP_VAI_SUFFIXES
! Singular INDEPENDENT
@R.person.NI@+1Sg:@R.person.NI@0   		# ;	
@R.person.KI@+2Sg:@R.person.KI@0   		# ;
@R.person.NULL@+3Sg:@R.person.NULL@%>wa		# ;
@R.person.NULL@+3Sg:@R.person.NULL@%>w		# ;
@R.person.NULL@+4Sg:@R.person.NULL@%>yini   	# ;
! Plural INDEPENDENT
@R.person.NI@+1Pl:@R.person.NI@%>hpinnaana  # ;	
@R.person.NI@+1Pl:@R.person.NI@%>hpinnaan  # ;	
@R.person.NULL@+21Pl:@R.person.NULL@%>o'pa	 # ;	
@R.person.NULL@+21Pl:@R.person.NULL@%>o'p	 # ;
@R.person.KI@+2Pl:@R.person.KI@%>hpoaawa	 # ;  	
@R.person.NULL@+3Pl:@R.person.NULL@%>yi		# ;
@R.person.NULL@+4Pl:@R.person.NULL@%>yi   	# ;
@R.person.NULL@+3Pl:@R.person.NULL@%>y		# ;
@R.person.NULL@+4Pl:@R.person.NULL@%>y   	# ;

LEXICON CONJ_VAI_SUFFIXES
! Singular CONJUNCTIVE
@R.person.NI@+1Sg:@R.person.NI@%>hsi   	# ; 
@R.person.KI@+2Sg:@R.person.KI@%>hsi  	# ;
@R.person.OT@+3Sg:@R.person.OT@%>hsi 		# ;
@R.person.NULL@+4Sg:@R.person.NULL@%>hsi   	# ;
! Plural CONJUNCTIVE
@R.person.NI@+1Pl:@R.person.NI@%>hsinnaana  # ;
@R.person.NI@+1Pl:@R.person.NI@%>hsinnaan  # ;
@R.person.NULL@+21Pl:@R.person.NULL@%>hsi 	 # ;
@R.person.KI@+2Pl:@R.person.KI@%>hsoaayi	 # ;  
@R.person.OT@+3Pl:@R.person.OT@%>hsi	# ;	
@R.person.NULL@+4Pl:@R.person.NULL@%>hsi	# ;

LEXICON SUBJ_VAI_SUFFIXES
! Singular SUBJUNCTIVE
@R.person.NULL@+1Sg:@R.person.NULL@%>iniki   	# ;
@R.person.NULL@+2Sg:@R.person.NULL@%>iniki   	# ;
@R.person.NULL@+3Sg:@R.person.NULL@%>si   	# ;
@R.person.NULL@+4Sg:@R.person.NULL@%>si   	# ;
! Plural SUBJUNCTIVE
@R.person.NULL@+1Pl:@R.person.NULL@%>innaaniki  # ;
@R.person.NULL@+21Pl:@R.person.NULL@%>inoainiki # ;	
@R.person.NULL@+2Pl:@R.person.NULL@%>o'ki 		# ;
@R.person.NULL@+3Pl:@R.person.NULL@%>si			# ;
@R.person.NULL@+4Pl:@R.person.NULL@%>si			# ;

LEXICON UNREAL_VAI_SUFFIXES
! Singular UNREAL
@R.person.NI@+1Sg:@R.person.NI@%>htopi   	# ;
@R.person.KI@+2Sg:@R.person.KI@%>htopi   	# ;
@R.person.NULL@+3Sg:@R.person.NULL@%>wahtopi	# ;
@R.person.NULL@+4Sg:@R.person.NULL@%>wahtopini   # ;	
! Plural UNREAL
@R.person.NI@+1Pl:@R.person.NI@%>hpinnaanopi  # ;
@R.person.NULL@+21Pl:@R.person.NULL@%>hpoaawopi 	# ;	
@R.person.KI@+2Pl:@R.person.KI@%>wahtopiyi	# ;   
@R.person.NULL@+3Pl:@R.person.NULL@%>wahtopiyi	# ;
@R.person.NULL@+4Pl:@R.person.NULL@%>wahtopiyini    # ;

LEXICON IMP_VAI_SUFFIXES
! Singular IMPERATIVE
@R.person.IMP@+Sg:@R.person.IMP@%>t	# ;
! Plural IMPERATIVE
@R.person.IMP@+Pl:@R.person.IMP@%>k	# ;

! BELOW ARE THE VII SUFFIXES

LEXICON VII_SUFFIXES
@R.mode.IND@+Ind:@R.mode.IND@ INDEP_VII_SUFFIXES ;
@R.mode.CON@+Cnj:@R.mode.CON@ CONJ_VII_SUFFIXES ;
@R.mode.SUB@+Sbj:@R.mode.SUB@ SUBJ_VII_SUFFIXES ;
@R.mode.IRR@+Irr:@R.mode.IRR@ UNREAL_VII_SUFFIXES ;

LEXICON INDEP_VII_SUFFIXES
! Singular INDEPENDENT
@R.person.NULL@+0Sg:@R.person.NULL@%>wa		# ;
! Plural INDEPENDENT
@R.person.NULL@+0Pl:@R.person.NULL@%>yi		# ;

LEXICON CONJ_VII_SUFFIXES
! Singular CONJUNCTIVE
@R.person.OT@+0Sg:@R.person.OT@%>hsi 		# ;
! Plural CONJUNCTIVE
@R.person.OT@+0Pl:@R.person.OT@%>hsi	# ;	

LEXICON SUBJ_VII_SUFFIXES
! Singular SUBJUNCTIVE
@R.person.NULL@+0Sg:@R.person.NULL@%>si   	# ;
! Plural SUBJUNCTIVE
@R.person.NULL@+0Pl:@R.person.NULL@%>si			# ;

LEXICON UNREAL_VII_SUFFIXES
! Singular UNREAL
@R.person.NULL@+0Sg:@R.person.NULL@%>wahtopi	# ;
! Plural UNREAL  
@R.person.NULL@+0Pl:@R.person.NULL@%>wahtopiyi	# ;

! BELOW ARE THE VTI SUFFIXES 

LEXICON VTI_SUFFIXES
@R.mode.IND@+Ind:@R.mode.IND@	INDEP_VTI_SUFFIXES ;
@R.mode.CON@+Cnj:@R.mode.CON@	CONJ_VTI_SUFFIXES ;
@R.mode.SUB@+Sbj:@R.mode.SUB@	SUBJ_VTI_SUFFIXES ;
@R.mode.IRR@+Irr:@R.mode.IRR@	UNREAL_VTI_SUFFIXES ;
@R.mode.IMP@+Imp:@R.mode.IMP@	IMP_VTI_SUFFIXES ;

! Objects are included because Frantz says that -wa and -yi suffixes are only added to 1 and 2 forms in the independent.

LEXICON INDEP_VTI_SUFFIXES
! Singular INDEPENDENT
@R.person.NI@+1Sg+0Sg:@R.person.NI@%>hpa   		# ;	
@R.person.KI@+2Sg+0Sg:@R.person.KI@%>hpa   		# ;
@R.person.NI@+1Sg+0Pl:@R.person.NI@%>hpi   		# ;	
@R.person.KI@+2Sg+0Pl:@R.person.KI@%>hpi   		# ;
@R.person.NULL@+3Sg+0Sg:@R.person.NULL@%>ma		# ;
@R.person.NULL@+4Sg+0Sg:@R.person.NULL@%>mini   	# ;
! Plural INDEPENDENT
@R.person.NI@+1Pl+0Sg:@R.person.NI@%>hpinnaana  # ;	
@R.person.NULL@+21Pl+0Sg:@R.person.NULL@%>'pa	 # ;
@R.person.NI@+1Pl+0Pl:@R.person.NI@%>hpinnaani  # ;	
@R.person.NULL@+21Pl+0Pl:@R.person.NULL@%>'pi	 # ;
@R.person.KI@+2Pl+0Sg:@R.person.KI@%>hpoaawa	 # ;
@R.person.KI@+2Pl+0Pl:@R.person.KI@%>hpoaayi	 # ; 
@R.person.NULL@+3Pl+0Pl:@R.person.NULL@%>mi		# ;
@R.person.NULL@+4Pl+0Pl:@R.person.NULL@%>mi   	# ;

LEXICON CONJ_VTI_SUFFIXES
! Singular CONJUNCTIVE
@R.person.NI@+1Sg+0Sg:@R.person.NI@%>hsi   	# ; 
@R.person.KI@+2Sg+0Sg:@R.person.KI@%>hsi  	# ;
@R.person.OT@+3Sg+0Sg:@R.person.OT@%>hsi 		# ;
@R.person.NULL@+4Sg+0Sg:@R.person.NULL@%>hsi   	# ;
@R.person.NI@+1Sg+0Pl:@R.person.NI@%>hsi   	# ; 
@R.person.KI@+2Sg+0Pl:@R.person.KI@%>hsi  	# ;
@R.person.OT@+3Sg+0Pl:@R.person.OT@%>hsi 		# ;
@R.person.NULL@+4Sg+0Pl:@R.person.NULL@%>hsi   	# ;
! Plural CONJUNCTIVE
@R.person.NI@+1Pl+0Sg:@R.person.NI@%>hsinnaani  # ;
@R.person.NI@+1Pl+0Sg:@R.person.NI@%>hsinnaan  # ;
@R.person.NULL@+21Pl+0Sg:@R.person.NULL@%>hsi 	 # ;
@R.person.KI@+2Pl+0Sg:@R.person.KI@%>hsoaayi	 # ;  
@R.person.OT@+3Pl+0Sg:@R.person.OT@%>hsi	# ;	
@R.person.YI@+4Pl+0Sg:@R.person.YI@%>hsi	# ;
@R.person.NI@+1Pl+0Pl:@R.person.NI@%>hsinnaani  # ;
@R.person.NI@+1Pl+0Pl:@R.person.NI@%>hsinnaan  # ;
@R.person.NULL@+21Pl+0Pl:@R.person.NULL@%>hsi 	 # ;
@R.person.KI@+2Pl+0Pl:@R.person.KI@%>hsoaayi	 # ;  
@R.person.OT@+3Pl+0Pl:@R.person.OT@%>hsi	# ;	
@R.person.YI@+4Pl+0Pl:@R.person.YI@%>hsi	# ;

LEXICON SUBJ_VTI_SUFFIXES
! Singular SUBJUNCTIVE
@R.person.NULL@+1Sg+0Sg:@R.person.NULL@%>mmiiniki   	# ;
@R.person.NULL@+2Sg+0Sg:@R.person.NULL@%>mmiiniki   	# ;
@R.person.NULL@+3Sg+0Sg:@R.person.NULL@%>isi   	# ;
@R.person.NULL@+4Sg+0Sg:@R.person.NULL@%>isi   	# ;
@R.person.NULL@+1Sg+0Pl:@R.person.NULL@%>mmiiniki   	# ;
@R.person.NULL@+2Sg+0Pl:@R.person.NULL@%>mmiiniki   	# ;
@R.person.NULL@+3Sg+0Pl:@R.person.NULL@%>isi   	# ;
@R.person.NULL@+4Sg+0Pl:@R.person.NULL@%>isi   	# ;
! Plural SUBJUNCTIVE
@R.person.NULL@+1Pl+0Sg:@R.person.NULL@%>mmiiinnainiki  # ;
@R.person.NULL@+21Pl+0Sg:@R.person.NULL@%>mmiinoainiki # ;	
@R.person.NULL@+2Pl+0Sg:@R.person.NULL@%>i'ki 		# ;
@R.person.NULL@+3Pl+0Sg:@R.person.NULL@%>isi			# ;
@R.person.NULL@+4Pl+0Sg:@R.person.NULL@%>isi			# ;
@R.person.NULL@+1Pl+0Pl:@R.person.NULL@%>mmiiinnainiki  # ;
@R.person.NULL@+21Pl+0Pl:@R.person.NULL@%>mmiinoainiki # ;	
@R.person.NULL@+2Pl+0Pl:@R.person.NULL@%>i'ki 		# ;
@R.person.NULL@+3Pl+0Pl:@R.person.NULL@%>isi			# ;
@R.person.NULL@+4Pl+0Pl:@R.person.NULL@%>isi			# ;

LEXICON UNREAL_VTI_SUFFIXES
! Singular UNREAL
@R.person.NI@+1Sg+0Sg:@R.person.NI@%>htopi   	# ;
@R.person.KI@+2Sg+0Sg:@R.person.KI@%>htopi   	# ;
@R.person.NULL@+3Sg+0Sg:@R.person.NULL@%>mahtopi	# ;
@R.person.NULL@+4Sg+0Sg:@R.person.NULL@%>mahtopiyini   # ;
@R.person.NI@+1Sg+0Pl:@R.person.NI@%>htopi   	# ;
@R.person.KI@+2Sg+0Pl:@R.person.KI@%>htopi   	# ;
@R.person.NULL@+3Sg+0Pl:@R.person.NULL@%>mahtopi	# ;
@R.person.NULL@+4Sg+0Pl:@R.person.NULL@%>mahtopiyini   # ;	
! Plural UNREAL
@R.person.NI@+1Pl+0Sg:@R.person.NI@%>hpinnaanopi  # ;
@R.person.NULL@+21Pl+0Sg:@R.person.NULL@%>'pohtopi 	# ;	
@R.person.KI@+2Pl+0Sg:@R.person.KI@%>mahtopiyi	# ;   
@R.person.NULL@+3Pl+0Sg:@R.person.NULL@%>mahtopiyi	# ;
@R.person.NULL@+4Pl+0Sg:@R.person.NULL@%>mahtopiyini    # ;
@R.person.NI@+1Pl+0Pl:@R.person.NI@%>hpinnaanopi  # ;
@R.person.NULL@+21Pl+0Pl:@R.person.NULL@%>'pohtopi 	# ;	
@R.person.KI@+2Pl+0Pl:@R.person.KI@%>mahtopiyi	# ;   
@R.person.NULL@+3Pl+0Pl:@R.person.NULL@%>mahtopiyi	# ;
@R.person.NULL@+4Pl+0Pl:@R.person.NULL@%>mahtopiyini    # ;

LEXICON IMP_VTI_SUFFIXES
! Singular IMPERATIVE
@R.person.IMP@+2Sg+0Sg:@R.person.IMP@%>ta	# ;
@R.person.IMP@+2Sg+0Pl:@R.person.IMP@%>ti	# ;
! Plural IMPERATIVE
@R.person.IMP@+2Pl+0Sg:@R.person.IMP@%>ka	# ;
@R.person.IMP@+2Pl+0Pl:@R.person.IMP@%>ki	# ;

! BELOW ARE THE VTA VERBS

LEXICON VTA_SUFFIXES
@R.mode.IND@+Ind:@R.mode.IND@	INDEP_VTA_SUFFIXES ;
@R.mode.CON@+Cnj:@R.mode.CON@	CONJ_VTA_SUFFIXES ;
@R.mode.SUB@+Sbj:@R.mode.SUB@	SUBJ_VTA_SUFFIXES ;
@R.mode.IMP@+Imp:@R.mode.IMP@	IMP_VTA_SUFFIXES ;

! I will first list the DIRECT paradigms

LEXICON INDEP_VTA_SUFFIXES
! Direct, Singular Subject and Object, INDEPENDENT
@R.person.KI@+1Sg+2Sg:@R.person.KI@%>o   		# ;	
@R.person.NI@+1Sg+3Sg:@R.person.NI@%>aawa   	# ;
@R.person.NI@+1Sg+4Sg:@R.person.NI@%>aayini   		# ;
@R.person.KI@+2Sg+3Sg:@R.person.KI@%>aawa   		# ;
@R.person.KI@+2Sg+4Sg:@R.person.KI@%>aayini		# ;
@R.person.KI@+2Sg+4Pl:@R.person.KI@%>aayini		# ;
@R.person.NULL@+3Sg+4Sg:@R.person.NULL@%>yiiwa   	# ;
@R.person.NULL@+4Sg+5:@R.person.NULL@%>yiiyina   	# ;
! Direct, Plural Subject, Singular Object, INDEPENDENT
@R.person.KI@+1Pl+2Sg:@R.person.KI@%>ohpinnaana  		# ;	
@R.person.NI@+1Pl+3Sg:@R.person.NI@%>annanna   	# ;
@R.person.NI@+1Pl+4Sg:@R.person.NI@%>annaanina   		# ;	
@R.person.KI@+2Pl+3Sg:@R.person.KI@%>awaawa   		# ;
@R.person.KI@+2Pl+4Sg:@R.person.KI@%>awaayini		# ;
@R.person.NULL@+21Pl+3Sg:@R.person.NULL@%>aawa   	# ;
@R.person.NULL@+3Pl+4Sg:@R.person.NULL@%>yiiyi   	# ;
! Direct, Plural Subject and Object, INDEPENDENT
@R.person.KI@+1Pl+2Pl:@R.person.KI@%>ohpinnaana  		# ;	
@R.person.NI@+1Pl+3Pl:@R.person.NI@%>annaani  # ;	
@R.person.NI@+1Pl+4Pl:@R.person.NI@%>annaani  # ;	
@R.person.KI@+2Pl+3Pl:@R.person.KI@%>awaayi	 # ;
@R.person.KI@+2Pl+4Pl:@R.person.KI@%>awaayi	 # ;
@R.person.NULL@+21Pl+3Pl:@R.person.NULL@%>aayi  # ;
@R.person.NULL@+3Pl+4Pl:@R.person.NULL@%>yiiyi   	# ;
@R.person.NULL@+4Pl+5:@R.person.NULL@%>yiiyi   	# ;
! Direct, Plural Object, Singular Subject
@R.person.KI@+1Sg+2Pl:@R.person.KI@%>ohpoaawa  		# ;	
@R.person.KI@+1Sg+2Pl:@R.person.KI@%>ohpoaaw  		# ;	
@R.person.NI@+1Sg+3Pl:@R.person.NI@%>aayi   	# ;
@R.person.NI@+1Sg+4Pl:@R.person.NI@%>aayi   	# ;
@R.person.KI@+2Sg+3Pl:@R.person.KI@%>aayi   		# ;
@R.person.KI@+2Sg+4Pl:@R.person.KI@%>aayi   		# ;
@R.person.NULL@+3Sg+4Pl:@R.person.NULL@%>yiiwa   	# ;
@R.person.NULL@+4Sg+5:@R.person.NULL@%>yiiyini   	# ;
! Inverse, Singular Subject and Object, INDEPENDENT
@R.person.KI@+2Sg+1Sg:@R.person.KI@%>oki   		# ;	
@R.person.KI@+3Sg+2Sg:@R.person.KI@%>oka   	# ;
@R.person.NI@+3Sg+1Sg:@R.person.NI@%>oka  		# ;	
@R.person.OT@+4Sg+3Sg:@R.person.OT@%>oka   		# ;
@R.person.KI@+4Sg+2Sg:@R.person.KI@%>okini   	# ;
@R.person.NI@+4Sg+1Sg:@R.person.NI@%>okini   	# ;
@R.person.OT@+4Sg+3Sg:@R.person.OT@%>oka   		# ;
@R.person.OT@+5+4Sg:@R.person.OT@%>okini   	# ;
! Inverse, Plural Subject, Singular Object, INDEPENDENT
@R.person.KI@+2Pl+1Sg:@R.person.KI@%>okihpoaawa  		# ;
@R.person.NI@+3Pl+1Sg:@R.person.NI@%>oki   	# ;
@R.person.KI@+3Pl+2Sg:@R.person.KI@%>oki   	# ;
@R.person.NI@+4Pl+1Sg:@R.person.NI@%>oki   	# ;
@R.person.KI@+4Pl+2Sg:@R.person.KI@%>oki   	# ;
@R.person.OT@+4Pl+3Sg:@R.person.OT@%>oka   		# ;
@R.person.NULL@+4Pl+5:@R.person.NULL@%>yiiyini   		# ;
! Inverse, Plural Subject and Object, INDEPENDENT
@R.person.KI@+2Pl+1Pl:@R.person.KI@%>okihpinnana  # ;
@R.person.NULL@+3Pl+21Pl:@R.person.NULL@%>okiyi	 # ;
@R.person.NI@+3Pl+1Pl:@R.person.NI@%>okinnaani  # ;
@R.person.KI@+3Pl+2Pl:@R.person.KI@%>okoaayi  # ;
@R.person.OT@+4Pl+3Pl:@R.person.OT@%>okoaayi  # ;
@R.person.KI@+4Pl+2Pl:@R.person.KI@%>okoaayi  # ;
@R.person.NULL@+4Pl+21Pl:@R.person.NULL@%>okiyi  # ;
@R.person.NI@+4Pl+1Pl:@R.person.NI@%>okinnani  # ;
@R.person.OT@+5+4Pl:@R.person.OT@%>okoaayini  # ;
! Inverse, Plural Object, Singular Subject, INDEPENDENT
@R.person.KI@+2Sg+1Pl:@R.person.KI@%>okihpinnana  # ; 
@R.person.KI@+3Sg+2Pl:@R.person.KI@%>okoaawa   	# ;
@R.person.NI@+3Sg+1Pl:@R.person.NI@%>okinnaani 		# ;	
@R.person.NI@+3Sg+1Pl:@R.person.NI@%>okinnaan 		# ;	
@R.person.OT@+4Sg+3Pl:@R.person.OT@%>okoaayi   	# ;
@R.person.KI@+4Sg+2Pl:@R.person.KI@%>okini   	# ;
@R.person.NULL@+4Sg+21Pl:@R.person.NULL@%>okiyini   	# ;
@R.person.NI@+4Sg+1Pl:@R.person.NI@%>okini   	# ;
! Unspecified INDEPENDENT
@R.person.NI@Unspec+1Sg:@R.person.NI@%>okoo   	# ;
@R.person.NI@Unspec+1Pl:@R.person.NI@%>otsspinnaana   	# ;
@R.person.KI@Unspec+2Sg:@R.person.KI@%>okoo   	# ;
@R.person.KI@Unspec+2Sg:@R.person.KI@%>otsspoaawa   	# ;
@R.person.NULL@Unspec+21Pl:@R.person.NULL@%>okiwa  	# ;
@R.person.NULL@+Unspec+3Sg:@R.person.NULL@%>aawa   	# ;
@R.person.NULL@+Unspec+3Pl:@R.person.NULL@%>aayi   	# ;
@R.person.NULL@+Unspec+4Sg:@R.person.NULL@%>aayini   	# ;
@R.person.NULL@+Unspec+4Pl:@R.person.NULL@%>aayi   	# ;

LEXICON CONJ_VTA_SUFFIXES
! Direct, Singular Subject and Object, CONJUNCTIVE
@R.person.KI@+1Sg+2Sg:@R.person.KI@%>ohsi   	# ;	
@R.person.NI@+1Sg+3Sg:@R.person.NI@%>ahsi   	# ;
@R.person.KI@+2Sg+3Sg:@R.person.KI@%>ahsi   		# ;
@R.person.OT@+3Sg+4Sg:@R.person.OT@%>ahsi   		# ;
! Direct, Plural Subject, Singular Object, CONJUNCTIVE
@R.person.KI@+1Pl+2Sg:@R.person.KI@%>ohsinnaani   		# ;
@R.person.NI@+1Pl+3Sg:@R.person.NI@%>ahsinnaani   		# ;
@R.person.NI@+1Pl+4Sg:@R.person.NI@%>ahsinnaanini   		# ;
@R.person.NULL@+21Pl+3Sg:@R.person.NULL@%>ahsi   		# ;
@R.person.NULL@+21Pl+4Sg:@R.person.NULL@%>ahsini   		# ;
@R.person.KI@+2Pl+3Sg:@R.person.KI@%>ahsoaayi   		# ;
@R.person.KI@+2Pl+4Sg:@R.person.KI@%>ahsoaayini   		# ;
@R.person.OT@+3Pl+4Sg:@R.person.OT@%>ahsi   		# ;
! Direct, Plural Subject and Object, CONJUNCTIVE
@R.person.KI@+1Pl+2Pl:@R.person.KI@%>ohsinnaani   		# ;
@R.person.NI@+1Pl+3Pl:@R.person.NI@%>ahsinnaani   		# ;
@R.person.NI@+1Pl+4Pl:@R.person.NI@%>ahsinnaani   		# ;
@R.person.NULL@+21Pl+3Pl:@R.person.NULL@%>ahsi   		# ;
@R.person.NULL@+21Pl+4Pl:@R.person.NULL@%>ahsi   		# ;
@R.person.KI@+2Pl+3Pl:@R.person.KI@%>ahsoaayi   		# ;
@R.person.KI@+2Pl+4Pl:@R.person.KI@%>ahsoaayi   		# ;
@R.person.OT@+3Pl+4Pl:@R.person.OT@%>ahsi   		# ;
! Direct, Singular Subject, Plural Object, CONJUNCTIVE
@R.person.KI@+1Sg+2Pl:@R.person.KI@%>ohsoaayi   		# ;
@R.person.NI@+1Sg+3Pl:@R.person.NI@%>ahsi   	# ;
@R.person.NI@+1Sg+4Pl:@R.person.NI@%>ahsi   	# ;
@R.person.KI@+2Sg+3Pl:@R.person.KI@%>ahsi   		# ;
@R.person.KI@+2Sg+4Pl:@R.person.KI@%>ahsi   		# ;
@R.person.OT@+3Sg+4Pl:@R.person.OT@%>ahsi   		# ;
! Inverse, Singular Subject and Object, CONJUNCTIVE
@R.person.KI@+2Sg+1Sg:@R.person.KI@%>okssi   	# ;	
@R.person.KI@+3Sg+2Sg:@R.person.KI@%>yssi   	# ;
@R.person.NI@+3Sg+1Sg:@R.person.NI@%>yssi   		# ;
@R.person.OT@+4Sg+3Sg:@R.person.OT@%>yssi   		# ;
! Inverse, Plural Subject and Singular Object, CONJUNCTIVE
@R.person.KI@+2Pl+1Sg:@R.person.KI@%>okssoaayi   	# ;	
@R.person.NI@+3Pl+1Sg:@R.person.NI@%>yssinnaani   		# ;
@R.person.KI@+3Pl+2Sg:@R.person.KI@%>yssi   	# ;
@R.person.OT@+4Pl+3Sg:@R.person.OT@%>yssi   		# ;
! Inverse, Plural Subject and Object, CONJUNCTIVE
@R.person.KI@+2Pl+1Pl:@R.person.KI@%>okssinnaani   	# ;	
@R.person.KI@+3Pl+2Pl:@R.person.KI@%>yssoaayi   	# ;
@R.person.NI@+3Pl+21Pl:@R.person.NI@%>okssi   		# ;
@R.person.NI@+3Pl+1Pl:@R.person.NI@%>yssinnaani   		# ;
@R.person.OT@+4Pl+4Pl:@R.person.OT@%>yssi   		# ;
! Inverse, Singular Subject and Plural Object, CONJUNCTIVE
@R.person.KI@+2Sg+1Pl:@R.person.KI@%>okssinnaani   	# ;
@R.person.NI@+3Sg+1Pl:@R.person.NI@%>yssinnaani   	# ;
@R.person.KI@+3Sg+2Pl:@R.person.KI@%>yssoaayi   	# ;
@R.person.OT@+3Sg+2Pl:@R.person.OT@%>aahsi   	# ;
@R.person.OT@+4Sg+3Pl:@R.person.OT@%>yssi   	# ;
@R.person.OT@+4Sg+4Pl:@R.person.OT@%>yssi   	# ;
! Unspecified Subject, Conjunctive
@R.person.NI@+Unspec+1Sg:@R.person.NI@%>okoohsi   	# ;	
@R.person.NI@+Unspec+1Pl:@R.person.NI@%>otssinnaani   	# ;
@R.person.KI@+Unspec+2Sg:@R.person.KI@%>okoohsi   	# ;
@R.person.KI@+Unspec+2Pl:@R.person.KI@%>otssoaayi   	# ;
@R.person.NULL@+Unspec+21Pl:@R.person.NULL@%>otssi   	# ;
@R.person.NULL@+Unspec+3Sg:@R.person.NULL@%>aahsi   	# ;
@R.person.NULL@+Unspec+3Pl:@R.person.NULL@%>aahsi   	# ;
@R.person.NULL@+Unspec+4Sg:@R.person.NULL@%>aahsini   	# ;
@R.person.NULL@+Unspec+4Pl:@R.person.NULL@%>aahsi   	# ;

LEXICON SUBJ_VTA_SUFFIXES
! Direct, Singular Subject and Object, SUBJUNCTIVE
@R.person.NULL@+1Sg+2Sg:@R.person.NULL@%>iniki   	# ;	
@R.person.NULL@+1Sg+3Sg:@R.person.NULL@%>aniki   	# ;
@R.person.NULL@+2Sg+3Sg:@R.person.NULL@%>aniki   	# ;
@R.person.NULL@+3Sg+4Sg:@R.person.NULL@%>asi   	# ;	
! Direct, Plural Subject, Singular Object, SUBJUNCTIVE
@R.person.NULL@+1Pl+2Sg:@R.person.NULL@%>innaaniki   	# ;
@R.person.NULL@+1Pl+3Sg:@R.person.NULL@%>ainnaaniki   	# ;
@R.person.NULL@+2Pl+3Sg:@R.person.NULL@%>ainainiki   	# ;
@R.person.NULL@+21Pl+3Sg:@R.person.NULL@%>ahki   	# ;
@R.person.NULL@+3Pl+4Sg:@R.person.NULL@%>asi   	# ;	
! Direct, Plural Subject and Object, SUBJUNCTIVE
@R.person.NULL@+1Pl+2Pl:@R.person.NULL@%>innaaniki   	# ;
@R.person.NULL@+1Pl+3Pl:@R.person.NULL@%>ainnaaniki   	# ;
@R.person.NULL@+2Pl+3Pl:@R.person.NULL@%>ainainiki   	# ;
R.person.NULL@+21Pl+3Pl:@R.person.NULL@%>ahki   	# ;
@R.person.NULL@+3Pl+4Pl:@R.person.NULL@%>asi   	# ;	
! Direct, Singular Subject, Plural Object, SUBJUNCTIVE
@R.person.NULL@+1Sg+2Pl:@R.person.NULL@%>inoainiki   	# ;
@R.person.NULL@+2Sg+3Pl:@R.person.NULL@%>ainiki   	# ;
@R.person.NULL@+3Sg+4Pl:@R.person.NULL@%>asi   	# ;	
! Inverse, Singular Subject and Object, SUBJUNCTIVE
@R.person.NULL@+2Sg+1Sg:@R.person.NULL@%>okiiniki   	# ;	
@R.person.NULL@+3Sg+1Sg:@R.person.NULL@%>otsiiniki   	# ;
@R.person.NULL@+3Sg+2Sg:@R.person.NULL@%>otsiiniki   	# ;
@R.person.NULL@+4Sg+3Sg:@R.person.NULL@%>otsiiniki   	# ;
! Inverse, Plural Subject and Singular Object, SUBJUNCTIVE
@R.person.NULL@+2Pl+1Sg:@R.person.NULL@%>okiinoainiki   	# ;
@R.person.NULL@+3Pl+1Sg:@R.person.NULL@%>otsiiniki   	# ;
@R.person.NULL@+3Pl+2Sg:@R.person.NULL@%>otsiiniki   	# ;
! Inverse, Plural Subject and Object, SUBJUNCTIVE
@R.person.NULL@+2Pl+1Pl:@R.person.NULL@%>okinnaaniki   	# ;	
@R.person.NULL@+3Pl+2Pl:@R.person.NULL@%>otsiinoainiki   	# ;
@R.person.NULL@+3Pl+1Pl:@R.person.NULL@%>otsiinoainiki   	# ;
@R.person.NULL@+3Pl+1Pl:@R.person.NULL@%>otsiinnaaniki   	# ;
@R.person.NULL@+3Pl+21Pl:@R.person.NULL@%>okisi   	# ;
! Inverse, Singular Subject and Plural Object, CONJUNCTIVE
@R.person.NULL@+3Sg+1Pl:@R.person.NULL@%>okinnaaniki   	# ;	
@R.person.NULL@+3Sg+2Pl:@R.person.NULL@%>okinnaaniki   	# ;
@R.person.NULL@+3Sg+21Pl:@R.person.NULL@%>okisi   	# ;
! Unspecified Subject, SUBJUNCTIVE
@R.person.NULL@+Unspec+1Sg:@R.person.NULL@%>okoiniki   	# ;	
@R.person.NULL@+Unspec+1Pl:@R.person.NULL@%>okoinnaaniki   	# ;
@R.person.NULL@+Unspec+2Sg:@R.person.NULL@%>okoiniki   	# ;	
@R.person.NULL@+Unspec+2Pl:@R.person.NULL@%>okoinoainiki		# ;
@R.person.NULL@+Unspec+21Pl:@R.person.NULL@%>otsski   	# ;
@R.person.NULL@+Unspec+3Sg:@R.person.NULL@%>aahki   	# ;
@R.person.NULL@+Unspec+3Pl:@R.person.NULL@%>aahki   	# ;

LEXICON IMP_VTA_SUFFIXES
! Singular IMPERATIVE
@R.person.IMP@+2Sg+1Sg:@R.person.IMP@%>okit         # ;
@R.person.IMP@+2Sg+1Pl:@R.person.IMP@%>okinnaana # ;
@R.person.IMP@+2Sg+3Sg:@R.person.IMP@%>is        # ;
@R.person.IMP@+2Sg+3Pl:@R.person.IMP@%>is        # ;
! Plural IMPERATIVE
@R.person.IMP@+2Pl+1Sg:@R.person.IMP@%>okik         # ;
@R.person.IMP@+2Pl+1Pl:@R.person.IMP@%>okinnaana # ;
@R.person.IMP@+2Pl+3Sg:@R.person.IMP@%>ok        # ;
@R.person.IMP@+2Pl+3Pl:@R.person.IMP@%>ok        # ;

! END CODE
